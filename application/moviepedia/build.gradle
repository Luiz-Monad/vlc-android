apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'org.videolan.moviepedia'
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        buildConfig = true
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
    }

    defaultConfig {
        compileSdkVersion rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion

        consumerProguardFiles 'consumer-rules.pro'
        multiDexEnabled true

        buildConfigField 'int', 'VLC_VERSION_CODE', "${rootProject.ext.versionCode}"
        buildConfigField "String", "MOVIEPEDIA_API_URL", "\"${getMoviepediaUrl(project)}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
        dev {
            initWith debug
            matchingFallbacks = ['debug']
        }
        noTv {
            initWith release
            matchingFallbacks = ['release']
        }
        vlcBundle {
            initWith release
            matchingFallbacks = ['release']
        }
    }
}

static def getMoviepediaUrl(project) {
    if (project.hasProperty('moviepedia_api_url')) return project.properties['moviepedia_api_url'] else return  "https://localhost/"
}

dependencies {
    implementation project(':application:tools')
    implementation project(':application:vlc-android')

    // Provide proper JDBC version, see https://issuetracker.google.com/issues/174695268

    kapt "androidx.room:room-compiler:$rootProject.ext.androidxRoomVersion"
    kapt "org.xerial:sqlite-jdbc:$rootProject.ext.sqliteVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.ext.androidxLifecycleVersion"
    implementation "androidx.paging:paging-runtime-ktx:$rootProject.ext.androidxPagingVersion"
    implementation "androidx.room:room-runtime:$rootProject.ext.androidxRoomVersion"
    implementation "com.squareup.moshi:moshi-adapters:$rootProject.ext.moshiVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttp3Version"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.ext.kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.ext.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.kotlinx_version"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitExtVersion"
}

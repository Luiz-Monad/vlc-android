apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    namespace 'org.videolan.vlc'
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        buildConfig = true
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
    }

    defaultConfig {
        compileSdkVersion rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "org.videolan.vlc.MultidexTestRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        consumerProguardFiles 'consumer-rules.pro'
        multiDexEnabled true

        buildConfigField "String", "APP_ID", "\"${rootProject.ext.appId}\""
        buildConfigField 'String', 'LIBVLC_VERSION', "\"${rootProject.ext.libvlcVersion}\""
        buildConfigField 'String', 'ML_VERSION', "\"${rootProject.ext.mediaLibVersion}\""
        buildConfigField 'String', 'VLC_VERSION_NAME', "\"${rootProject.ext.versionName}\""
        buildConfigField 'String[]', 'TRANSLATION_ARRAY', generateTranslation()
        buildConfigField 'int', 'VLC_VERSION_CODE', "${rootProject.ext.versionCode}"
        resValue "string", "build_host", hostName()
        resValue "string", "build_revision", revision()
        resValue "string", "build_time", buildTime()
        resValue "string", "changelog", changelog()
        resValue 'string', 'tv_provider_authority', "${rootProject.ext.appId}.tv"
    }

    testOptions {
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jvmArgs '-noverify'
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            buildConfigField "boolean", "BETA", isBeta()
            resValue 'string', 'benchmark_package_name', 'org.videolan.vlcbenchmark'
        }
        signedRelease {
            initWith release
            matchingFallbacks = ['release']
        }
        debug {
            jniDebuggable true
            buildConfigField "String", "APP_ID", "\"${rootProject.ext.appId}.debug\""
            buildConfigField "boolean", "BETA", "false"
            buildConfigField "boolean", "NO_TV", "false"
            resValue 'string', 'benchmark_package_name', 'org.videolan.vlcbenchmark.debug'
            resValue 'string', 'tv_provider_authority', "${rootProject.ext.appId}.debug.tv"
        }
        dev {
            initWith debug
            matchingFallbacks = ['debug']
        }
        noTv {
            initWith release
            matchingFallbacks = ['release']
            buildConfigField "boolean", "NO_TV", "true"
        }
        vlcBundle {
            initWith release
            matchingFallbacks = ['release']
        }
    }

    sourceSets.main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src']
        resources.srcDirs = ['src']
        aidl.srcDirs = ['src']
        renderscript.srcDirs = ['src']
        res.srcDirs = ['res']
        assets.srcDirs = ['assets']
    }
    sourceSets.debug {
        manifest.srcFile 'flavors/debug/AndroidManifest.xml'
        res.srcDirs = ['flavors/debug/res']
        assets.srcDirs = ['flavors/debug/assets']
    }
    sourceSets.dev {
        manifest.srcFile 'test/AndroidManifest.xml'
        res.srcDirs = ['flavors/debug/res']
        assets.srcDirs = ['flavors/debug/assets']
    }
    sourceSets.test {
        manifest.srcFile 'test/AndroidManifest.xml'
        java.srcDirs = ['test', 'test-common']
        assets.srcDirs = ['flavors/debug/assets']
    }
    sourceSets.androidTest {
        java.srcDirs = ['androidTest', 'test-common']
        assets.srcDirs = ['flavors/debug/assets']
        assets.srcDirs += files("$projectDir/assets/schemas".toString())
    }
}

def generateTranslation() {
    def foundLocales = new StringBuilder()
    foundLocales.append("new String[]{")

    fileTree("../resources/src/main/res").visit { /*FileVisitDetails*/ details ->
        println 'details: ' + details
        if (details.file.path.endsWith("strings.xml")) {
            def languageCode = details.file.parentFile.name.replaceAll('values-', '').replaceAll('-r', '-')
            languageCode = (languageCode == "values") ? "en" : languageCode
            foundLocales.append("\"").append(languageCode).append("\"").append(",")
        }
    }

    foundLocales.append("}")
    //Don't forget to remove the trailing comma
    def foundLocalesString = foundLocales.toString().replaceAll(',}', '}')
    return foundLocalesString
}

tasks.register('generateSources', Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    api project(':application:live-plot-graph')
    api project(':application:mediadb')
    api project(':application:resources')
    api project(':application:tools')
    api project(':extension-api')
    api project(':libvlcjni:libvlc')
    api project(':medialibrary')
//    debugApi "org.videolan.android:libvlc-all:$rootProject.ext.libvlcVersion"
//    debugApi "org.videolan.android:medialibrary-all:$rootProject.ext.mediaLibVersion"
//    devApi project(':libvlcjni:libvlc')
//    devApi project(':medialibrary')
//    releaseApi "org.videolan.android:libvlc-all:$rootProject.ext.libvlcVersion"
//    releaseApi "org.videolan.android:medialibrary-all:$rootProject.ext.mediaLibVersion"
//    signedReleaseApi "org.videolan.android:libvlc-all:$rootProject.ext.libvlcVersion"
//    signedReleaseApi "org.videolan.android:medialibrary-all:$rootProject.ext.mediaLibVersion"

    kapt "androidx.room:room-compiler:$rootProject.ext.roomVersion"
    kapt "org.xerial:sqlite-jdbc:$rootProject.ext.sqliteVersion"
    api "androidx.activity:activity-ktx:$rootProject.ext.androidxActivityVersion"
    api "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"
    api "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintLayoutVersion"
    api "androidx.fragment:fragment-ktx:$rootProject.ext.androidxFragmentVersion"
    api "androidx.leanback:leanback-preference:$rootProject.ext.androidxLeanbackVersion"
    api "androidx.leanback:leanback:$rootProject.ext.androidxLeanbackVersion"
    api "androidx.lifecycle:lifecycle-common-java8:$rootProject.ext.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.ext.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-process:$rootProject.ext.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.ext.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-service:$rootProject.ext.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.ext.lifecycleVersion"
    api "androidx.media:media:1.6.0"
    api "androidx.paging:paging-runtime-ktx:$rootProject.ext.pagingVersion"
    api "androidx.recyclerview:recyclerview:$rootProject.ext.androidxRecyclerviewVersion"
    api "androidx.room:room-runtime:$rootProject.ext.roomVersion"
    api "androidx.tvprovider:tvprovider:$rootProject.ext.androidxLeanbackVersion"
    api "androidx.viewpager2:viewpager2:$rootProject.ext.viewPager2Version"
    api "androidx.window:window:1.0.0"
    api "com.google.android.material:material:$rootProject.ext.androidxMaterialVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$rootProject.ext.kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.kotlinx_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.kotlinx_version"
    api 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'nl.dionsegijn:konfetti:1.2.2'
    testImplementation "androidx.arch.core:core-testing:$rootProject.ext.archVersion"
    testImplementation "androidx.test:core:$rootProject.ext.testCore"
    testImplementation "com.jraska.livedata:testing-ktx:$rootProject.ext.livedataTest"
    testImplementation "io.mockk:mockk:$rootProject.ext.mockk"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.ext.kotlinx_version"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockito"
    testImplementation "org.powermock:powermock-api-mockito2:$rootProject.ext.powerMock"
    testImplementation "org.powermock:powermock-module-junit4:$rootProject.ext.powerMock"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectric"
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.ext.archVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.ext.roomVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitExtVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.testCore"
    androidTestImplementation 'com.jraska:falcon:2.2.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.0'
    androidTestUtil "androidx.test:orchestrator:$rootProject.ext.orchestrator"
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

static def hostName() {
    return "${System.getProperty("user.name")}@${InetAddress.localHost.hostName}"
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}

/**
 * Generate a changelog string from the NEWS file
 * @return a string containing the latest changelog entry
 */
def changelog() {
    def newsFile = new File("NEWS")
    def line, output = "", started = false
    if (newsFile.exists()) {
        newsFile.withReader { reader ->
            while ((line = reader.readLine()) != null) {
                if (started && !line?.trim()) break
                if (started) {
                    if (output != "") output += "\\n"
                    output += line.trim()
                }
                if (line.contains("---")) started = true
            }
        }
    }
    return output
}

def isBeta() {
    def versionNameLower = versionName.toLowerCase()
    return (versionNameLower.contains("beta") || versionNameLower.contains("rc") || versionNameLower.contains("alpha") || versionNameLower.contains("dev")).toString()
}

kapt {
    javacOptions {
        // Increase the max count of errors from annotation processors.
        // Default is 100.
        option("-Xmaxerrs", 500)
    }
}

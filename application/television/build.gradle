apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    namespace 'org.videolan.television'
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        buildConfig = true
    }

    dataBinding {
        enabled = true
    }

    viewBinding {
        enabled = true
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
    }

    defaultConfig {
        compileSdkVersion rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion

        consumerProguardFiles 'consumer-rules.pro'
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
        dev {
            initWith debug
            matchingFallbacks = ['debug']
        }
        noTv {
            initWith release
            matchingFallbacks = ['release']
        }
        vlcBundle {
            initWith release
            matchingFallbacks = ['release']
        }
    }
}

dependencies {
    implementation project(':application:vlc-android')
    api project(':application:moviepedia')

    implementation "com.jaredrummler:colorpicker:$rootProject.ext.colorpickerVersion"
    testImplementation "androidx.test:core:$rootProject.ext.testCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitExtVersion"
}

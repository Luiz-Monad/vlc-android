/*
 * *************************************************************************
 *  build.gradle.java
 * **************************************************************************
 *  Copyright © 2015 VLC authors and VideoLAN
 *  Author: Geoffrey Métais
 *
 *  This program is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program; if not, write to the Free Software
 *  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston MA 02110-1301, USA.
 *  ***************************************************************************
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def abi = System.getenv('GRADLE_ABI')?.toLowerCase() ?: "all"
ext {
    library_version = "$rootProject.ext.mediaLibVersion"
}

android {
    namespace 'org.videolan.medialibrary'
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        buildConfig = true
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        compileSdkVersion rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion

        consumerProguardFiles 'consumer-rules.pro'
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
        dev {
            initWith debug
            matchingFallbacks = ['debug']
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'jni/libs' // Where generated .so files are placed.
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets', 'libcompat/libs/armeabi']
        }
        test {
            manifest.srcFile 'test/AndroidManifest.xml'
            java.srcDirs = ['test']
        }
        androidTest {
            java.srcDirs = ['androidTest']
            assets.srcDirs += files("$projectDir/assets/schemas".toString())
        }
    }

    // Make per-variant version code
    libraryVariants.configureEach { variant ->
        //Custom APK name
        variant.outputs.each { output ->
            if (output.outputFileName != null && output.outputFileName.endsWith('.aar')) {
                output.outputFileName = "medialibrary-${abi}-${library_version}.aar"
            }
        }
    }
}

clean {
    delete 'build', 'jni/libs', 'jni/obj'
}

dependencies {
    releaseImplementation project(':libvlcjni:libvlc')
    devImplementation project(':libvlcjni:libvlc')
    debugImplementation "org.videolan.android:libvlc-all:$rootProject.ext.libvlcVersion"

    api "androidx.core:core:$rootProject.ext.androidxCoreVersion"
    api "androidx.fragment:fragment:$rootProject.ext.androidxFragmentVersion"
    api "androidx.legacy:legacy-support-v4:$rootProject.ext.androidxLegacyVersion"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
}
